plugins {
    id 'java-library'
    id 'com.palantir.git-version' version '0.12.2'
    id 'maven-publish'
    id 'signing'
    id 'jacoco'
}

////////////////////////////////
//  Build definition section  //
////////////////////////////////

group 'io.github.steromano87'

// Define version from Git tags
def details = versionDetails()
def versionString

if (project.hasProperty("mavenVersion")) {
    versionString = project.getProperty("mavenVersion")
} else {
    versionString = details.lastTag

    if (Integer.parseInt(details.commitDistance.toString()) > 0) {
        versionString += '+' + details.commitDistance.toString()
    }

    if (details.branchName != ~'(?:.+/)?master') {
        versionString += '-SNAPSHOT'
    }
}
version versionString

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    // Selenium client
    api group: 'org.seleniumhq.selenium', name: 'selenium-java', version: '3.141.59'

    // Appium client
    api group: 'io.appium', name: 'java-client', version: '7.2.0'

    // Javassist library for dynamic proxy creation
    implementation group: 'org.javassist', name: 'javassist', version: '3.26.0-GA'

    // Apache Commons Configuration
    implementation group: 'org.apache.commons', name: 'commons-configuration2', version: '2.6'

    // Apache Commons IO
    implementation group: 'commons-io', name: 'commons-io', version: '2.6'

    // Logging
    implementation group: 'org.slf4j', name: 'slf4j-api', version: '1.7.30'

    // Reflections
    implementation group: 'org.reflections', name: 'reflections', version: '0.9.11'

    // For URL building
    implementation group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.10'

    // JUnit 5 platform for testing
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter', version: '5.5.2'

    // Log4j2 SLF4J biding for test only
    testImplementation group: 'org.apache.logging.log4j', name: 'log4j-slf4j-impl', version: '2.13.0'
    testImplementation group: 'org.slf4j', name: 'jul-to-slf4j', version: '1.7.30'


    // TestContainers for Selenium tests (with Remote Web Driver)
    testImplementation group: 'org.testcontainers', name: 'selenium', version: '1.12.4'
    testImplementation group: 'org.testcontainers', name: 'junit-jupiter', version: '1.12.4'
    testImplementation group: 'org.seleniumhq.selenium', name: 'selenium-remote-driver', version: '3.141.59'
}

java {
    withJavadocJar()
    withSourcesJar()
}

compileJava {
    options.encoding = 'UTF-8'
    options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
}

test {
    useJUnitPlatform()

    if (project.hasProperty("dockerized")) {
        systemProperty "onigiri.page.web.baseurl", "file://"
        systemProperty "dockerized", "1"
    } else {
        systemProperty "onigiri.page.web.baseurl", "file://" + project.file("src/test/resources").absolutePath
        systemProperty "dockerized", "0"
    }
}

jacocoTestReport {
    reports {
        xml.enabled true
    }
}

// Add jacocoTestReport task after test to generate test coverage report
test.finalizedBy(jacocoTestReport)

javadoc {
    options {
        encoding = 'UTF-8'
        source = 8
        links += [
                'https://docs.oracle.com/javase/8/docs/api/',
                'https://seleniumhq.github.io/selenium/docs/api/java/',
                'https://www.javassist.org/html/',
                'https://commons.apache.org/proper/commons-configuration/apidocs/'
        ]
    }

    title "Onigiri $versionString API"

    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}


/////////////////////////////////////
//  Deployment definition section  //
/////////////////////////////////////

publishing {
    publications {
        onigiri(MavenPublication) {
            artifactId = 'onigiri'
            from components.java

            pom {
                name = 'Onigiri'
                description = 'Page Object Model framework for modern websites and native apps'
                url = 'https://steromano87.github.io/onigiri/'

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id = 'steromano87'
                        name = 'Stefano RomanÃ²'
                        email = 'rumix87@gmail.com'
                    }
                }

                scm {
                    connection = 'scm:git:git://github.com/steromano87/onigiri.git'
                    developerConnection = 'scm:git:ssh://git@github.com:steromano87/onigiri.git'
                    url = 'https://github.com/steromano87/onigiri'
                }
            }
        }
    }

    repositories {
        maven {
            def snapshotsRepoUrl = 'https://oss.sonatype.org/content/repositories/snapshots/'
            def releasesRepoUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'

            name 'OSSRH'
            url project.hasProperty('release') ? releasesRepoUrl : snapshotsRepoUrl
            credentials {
                username = findProperty('mavenUser')
                password = findProperty('mavenPassword')
            }
        }
    }
}

signing {
    sign publishing.publications.onigiri
}
